 TITLE 'TESTEXC test program for EXECCOMM'
TESTEXC  CSECT
         USING NUCON,0                                                  01110000
         STM   R14,R12,12(R13)                                          01120000
         LR    R12,R15                                                  01130000
         USING TESTEXC,R12        Set addressability
         LR    R2,R0              Incoming extended plist               01170000
         LR    R3,R1              Incoming tokenized plist              01180000
*                                                                       01190000
         LA    R0,(WORKSIZE+8)/8                                        01200000
         DMSFREE DWORDS=(0),TYPE=USER Allocate Work area                01210000
         LR    R6,R1              Hold WORKAREA address                 01220000
         LR    R0,R6                                                    01230000
         LA    R1,WORKSIZE                                              01240000
         SR    R5,R5                                                    01250000
         MVCL  R0,R4              Zero out the WORKAREA                 01260000
*                                                                       01270000
         ST    R13,4(,R6)         Chain                                 01280000
         ST    R6,8(,R13)            to                                 01290000
         LR    R13,R6                  new                              01300000
         USING WORKAREA,R13              savearea                       01310000
*                                                                       01320000
* Save the rest of the command line string.
*                                                                       01350000
         LM    R4,R5,4(R2)        Load begargs,endargs                  01360000
         SR    R5,R4                                                    01370000
         ST    R5,EPLENGTH        Save length of command args           01380000
         LTR   R5,R5                                                    01390000
         BZ    EPLSET                                                   01400000
*                                                                       01410000
         LA    R6,EPLLOCAL        Assume short EPLIST                   01420000
         C     R5,=A(80-1)        Unless too long                       01430000
         BNH   EPLCOPY            Save copy in our DSA                  01440000
         L     R5,=A(80-1)        Use max
EPLCOPY  DS    0H                                                       01500000
         ST    R6,EPLADDR                                               01510000
*                                                                       01520000
         MVI   0(R6),C' '         Blank out the area                    01530000
         MVC   1(*-*,R6),0(R6)     for size of EPLIST                   01540000
         EX    R5,*-6                plus one blank                     01550000
*                                                                       01560000
         BCTR  R5,0               Length - 1                            01570000
         MVC   0(*-*,R6),0(R4)                                          01580000
         EX    R5,*-6             Copy in arguments                     01590000
EPLSET   DS    0H                                                       01700000
         MVC   EPLNXT,EPLADDR     Set start of EPLIST                   01710000
         MVC   EPLLEFT,EPLENGTH   Set remaining bytes to scan           01720000
*                                 EPLTOKEN has next parm                01730000
*                                                                       01740000
* Retrieve a command of FETCH or SET
*                                                                       01760000
         LA    R4,=CL8'COMMAND'
         BAL   R14,NXTTOKEN       Get the next token                    01780000
         B     MISSING            Bif operand is missing                01790000
*                                                                       01800000
         MVC   COMMAND,EPLTOKEN   Remember command verb
         CLC   =C'FETCH',EPLTOKEN
         BE    VERBOK
         CLC   =C'SET',EPLTOKEN
         BNE   VERBERR            Nither FETCH or SET found
VERBOK   DS    0H
*
* Retrieve a variable name token
*
         LA    R4,=CL8'VARIABLE'
         BAL   R14,NXTTOKEN       Get the next token
         B     MISSING            Bif operand is missing
*
         MVC   VARNAME,EPLTOKEN
         LA    R4,VARNAME+7
FINDEND  DS    0H
         CLI   0(R4),C' '         Look for
         BNE   FOUNDIT              non-blank
         BCT   R4,FINDEND
FOUNDIT  DS    0H
         LA    R3,VARNAME
         SR    R4,R3              Found
         LA    R4,1(,R4)            length
         ST    R4,VARNAMEL
*
         CLC   =C'FETCH',COMMAND
         BE    FETCH              Parsing complete for fetch
*
* Retrieve rest of command line as variable contents
*
         LM    R0,R1,EPLLEFT EPLNXT EPLIST Scan ptrs
         LTR   R0,R0              Anything after Variable name
         BZ    NOOPTV             Missing value
         LA    R1,1(,R1)          If there it has to be
         BCTR  R0,0                  a blank which we skip
         LTR   R0,R0              Anything after the blank?
         BZ    NOOPTV             Missing value
*
         ST    R1,STRINGBF        Set buffer addr
         ST    R0,STRINGBL        Set length
*
*                                                                       01950000
         B     SET                Do the variable assign
*                                                                       02080000
*  Return to caller                                                     02090000
*                                                                       02100000
EXIT0    DS    0H                                                       02110000
         SR    R15,R15                                                  02120000
EXITRC   DS    0H                                                       02130000
         LR    R1,R13                                                   02140000
         L     R13,4(,R13)                                              02150000
         ST    R15,16(,R13)       Pass back the return code             02160000
         LA    R0,(WORKSIZE+8)/8                                        02170000
         DMSFRET DWORDS=(0),LOC=(1)                                     02180000
         LM    R14,R12,12(R13)                                          02190000
         BR    R14                                                      02200000
*------------------------------------------------------------*          08000000
*                                                            *          08010000
* Retrieve incoming information from the calling EXEC        *          08020000
*                                                            *          08030000
*------------------------------------------------------------*          08040000
FETCH    DS    0H
         USING SHVBLOCK,R5                                              08070000
*                                                                       08080000
* Retrieve input from a REXX variable                                   08090000
*                                                                       08100000
*                                                                       08130000
         LA    R1,VARNAME         REXX variable name address
         L     R0,VARNAMEL        REXX variable name length             08150000
         LA    R5,SHVAREA
         XC    SHVBLOCK(SHVBLEN),SHVBLOCK
         XC    SHEPLIST(16),SHEPLIST
*
         MVI   SHVCODE,SHVFETCH   Fetch variable value
         ST    R1,SHVNAMA         Set VAR name address
         ST    R0,SHVNAML                  and length
         LA    R2,BUFFER
*
         ST    R2,SHVVALA         Address of value buffer
         LA    R2,L'BUFFER
         ST    R2,SHVBUFL         Length of the buffer
*
         LA    R1,EXECCOMM        Fill in
         ST    R1,SHEPLIST           EPLIST
         ST    R5,SHEPL4          Word 4 -> SHVBLOCK
         LA    R0,SHEPLIST
         ICM   R1,B'1000',=X'02'
         SVC   202
         DC    AL4(1)
*
*              R1                 REXX variable value address           08170000
*              R0                 REXX variable value length            08180000
*                                 Return  R15 = return code             08760000
         LTR   R15,R15
         BNZ   EXITRC
*
         L     R4,SHVVALA         REXX variable value address
         L     R5,SHVVALL         REXX variable value length
*
         MVC   LINEDITL(LINEDISZ),LINEDITP Set pattern
         MVI   LINEDITB,255         Set buffer size in byte 1
         LINEDIT TEXT='Value: .........................................*
               ......................................',                *
               BUFFA=LINEDITB,DISP=TYPE,                               *
               SUB=(CHARA,((R4),(R5))),                                *
               MF=(E,LINEDITL)
         SR    R15,R15
         B     EXITRC
         DROP  R5                                                       08800000
*------------------------------------------------------------*          11510000
*                                                            *          11520000
* Send data to an EXEC caller                                *
*                                                            *          11540000
*------------------------------------------------------------*          11550000
SET      DS    0H
         USING SHVBLOCK,R5                                              11580000
*                                                                       11610000
* Send output to a REXX variable                                        11620000
*                                                                       11630000
*                                                                       11660000
         L     R3,STRINGBF        Set buffer addr
         L     R2,STRINGBL        Set length
         L     R0,VARNAMEL        REXX variable name length
         LA    R1,VARNAME         REXX variable name address
*
         LA    R5,SHVAREA
         XC    SHVBLOCK(SHVBLEN),SHVBLOCK
         XC    SHEPLIST(16),SHEPLIST
*
         MVI   SHVCODE,SHVSTORE   Call to set variable value
         ST    R1,SHVNAMA         Set VAR name address
         ST    R0,SHVNAML                  and length
         ST    R3,SHVVALA         Set VAR value address
         ST    R2,SHVVALL                  and length
*
         LA    R1,EXECCOMM        Fill in
         ST    R1,SHEPLIST           EPLIST
         ST    R5,SHEPL4          Word 4 -> SHVBLOCK
         LA    R0,SHEPLIST
         ICM   R1,B'1000',=X'02'
         SVC   202
         DC    AL4(1)
         B     EXITRC
*------------------------------------------------------------*
*                                                            *
*        Next Token Scan                                     *
*                                                            *
*------------------------------------------------------------*
NXTTOKEN DS    0H
         MVI   EPLTOKEN,C' '      Preblank target area
         MVC   EPLTOKEN+1(L'EPLTOKEN-1),EPLTOKEN
         LM    R0,R1,EPLLEFT EPLNXT Fetch EPLIST Scan ptrs
         LTR   R0,R0
         BZR   R14                Tell caller end of arguments
NXTTOK2  DS    0H
         CLI   0(R1),C' '         Found next non-blank?
         BNE   NXTTOK3            Bif so to scan token
         LA    R1,1(,R1)          To next character
         BCT   R0,NXTTOK2         Keep looking
         BR    R14                Tell caller end of arguments
NXTTOK3  DS    0H
         LR    R15,R1             Remember start of token
NXTTOK4  DS    0H
         CLI   0(R1),C' '         Found next non-blank?
         BE    NXTTOK5            Bif so to scan token
         LA    R1,1(,R1)          To next character
         BCT   R0,NXTTOK4         Keep looking
NXTTOK5  DS    0H
         STM   R0,R1,EPLLEFT EPLNXT Reset EPLIST Scan ptrs
         SR    R1,R15             Length of the token
         BCTR  R1,0
         MVC   EPLTOKEN(*-*),0(R15)
         EX    R1,*-6             Copy to 8 byte token area
         B     4(,R14)            Return argument tp caller
 TITLE 'Error exits'
*------------------------------------------------------------*          19300000
*                                                            *          19310000
* Error Exit routines                                        *          19320000
*                                                            *          19330000
*------------------------------------------------------------*          19340000
VERBERR  DS    0H
         MVC   LINEDITL(LINEDISZ),LINEDITP Set pattern
         MVI   LINEDITB,255       Set buffer size in byte 1
         LINEDIT TEXT='First token must be FETCH or SET',              *
               BUFFA=LINEDITB,DISP=TYPE,                               *
               MF=(E,LINEDITL)
         LA    R15,16
         B     EXITRC
MISSING  DS    0H                                                       20160000
         MVC   LINEDITL(LINEDISZ),LINEDITP Set pattern                  20170000
         MVI   LINEDITB,255       Set buffer size in byte 1
         LINEDIT TEXT='Missing ........ operand',                      *20190000
               BUFFA=LINEDITB,DISP=TYPE,                               *
               SUB=(CHARA,((R4),8)),                                   *20210000
               MF=(E,LINEDITL)                                          20220000
         LA    R15,16                                                   20230000
         B     EXITRC                                                   20240000
NOOPTV   DS    0H
         MVC   LINEDITL(LINEDISZ),LINEDITP Set pattern
         MVI   LINEDITB,255       Set buffer size in byte 1
         LINEDIT TEXT='No data to FETCH or SET',                       *
               BUFFA=LINEDITB,DISP=TYPE,                               *
               MF=(E,LINEDITL)
         LA    R15,16
         B     EXITRC
 TITLE 'Constants'
*                                                                       21440000
LINEDITP LINEDIT MF=L,MAXSUBS=5                                         21450000
LINEDISZ EQU   *-LINEDITP                                               21460000
*
*------------------------------------------------------------*          21480000
*  Constants and Literals                                    *          21490000
*------------------------------------------------------------*          21500000
         DS    0D                                                       21510000
EXECCOMM DC    CL8'EXECCOMM'                                            21520000
         DC    XL8'FFFFFFFFFFFFFFFF'                                    21530000
*                                                                       21540000
MAXWORD  DC    A(X'7FFFFFFF')     Maximum positive integer              21550000
*                                                                       21560000
         LTORG ,                                                        21570000
 TITLE 'Shared Variable Communications Block'
         SHVBLOCK ,                                                     21640000
*                                                                       21650000
 TITLE 'Work Area Definitions'
*------------------------------------------------------------*          21670000
*  Program Work Areas                                        *          21680000
*------------------------------------------------------------*          21690000
WORKAREA DSECT                                                          21700000
         DS    18A                                                      21710000
COMMAND  DS    CL8                Program command name
VARNAME  DS    CL8                Program variable name
VARLEN   DS    FL8                Program variable name length
*                                                                       21720000
DWORD    DS    D                  Doubleword work area                  21770000
*                                                                       21850000
STRINGBF DS    A                  Buffer for STRING value               21860000
STRINGBL DS    F                  Length of string buffer               21870000
*                                                                       21880000
CPBUFF   DS    A                  CP command response buffer            21890000
CPBUFFL  DS    F                  CP command buffer size                21900000
CPRESPNX DS    A                  CP response next line addr            21910000
CPRESPRL DS    F                  CP response remaining length          21920000
*                                                                       21930000
FILTERA  DS    A                  Ptr to AVOID/FIND/LOCATE              21940000
FILTERL  DS    F                    filter and length of it             21950000
*                                                                       21960000
MARGIN1  DS    F                  Selection column range                21970000
MARGIN2  DS    F                                                        21980000
*                                                                       21990000
ZONE1    DS    F                  Search zone start column              22000000
ZONE2    DS    F                  Search zone end column                22010000
*                                                                       22020000
VARNAMEA DS    A                  Address of VAR or STEM name           22030000
VARNAMEL DS    F                  Length of the name                    22040000
*                                                                       22280000
EPLENGTH DS    F                  Length of EPLIST args                 22290000
EPLADDR  DS    A                  Address of large EPLIST               22300000
EPLLEFT  DS    F                  Bytes left to scan in EPLIST          22310000
EPLNXT   DS    A                  Address of next byte to scan          22320000
EPLTOKEN DS    CL8                Last token scaned from EPLIST         22330000
EPLLOCAL DS    CL(2*80)           EPLIST saved uppercase copy           22340000
*                                 +EPLENGTH is lowercase copy           22350000
*                                                                       22360000
CVBSAVE  DS    8A                 Hold R14-R5 in CVB                    22370000
REXSAVE  DS    8A                 Hold R14-R5 in GETREXXV               22380000
*                                            and PUTREXXV               22390000
SAVEAREA DS    15A                Save area for R14-R12                 22400000
*                                                                       22410000
PLISTBLD DS    0D                 Various plists built here             22420000
         DS    5D                                                       22430000
LINEDITL LINEDIT MF=L,MAXSUBS=5                                         22440000
LINEDITB DS    CL133              LINEDIT BUFFER
*                                                                       22520000
SHEPLIST DS    A                  EPLIST for EXECCOMM call              22530000
         DS    A                                                        22540000
         DS    A                                                        22550000
SHEPL4   DS    A                                                        22560000
*                                                                       22570000
SHVAREA  DS    ((SHVBLEN+7)/8)D   SHVBLOCK build area                   22580000
*                                                                       22670000
         DS    0D                                                       22680000
BUFFER   DS    CL256                                                    22690000
WORKSIZE EQU   *-WORKAREA                                               22700000
         REGEQU ,                                                       22710000
         AFT ,                                                          22720000
         NUCON ,                                                        22730000
         END                                                            22740000
